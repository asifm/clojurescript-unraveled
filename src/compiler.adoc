=== Getting Started with the Compiler

At this point, you are surely very bored with the constant theoretical
explanations about the language itself and will want to write and
execute some code. The goal of this section is to provide a little
practical introduction to the _ClojureScript_ compiler.

The _ClojureScript_ compiler takes the source code that has been split
over numerous directories and namespaces and compiles it down to
JavaScript. Today, JavaScript has a great number of different
environments where it can be executed - each with its own
peculiarities.

For this case we are going to use shadow-cljs as a frontend for the
compiler. It is possible to use the compiler directly or use other
frontends, but in this case we will focus on just one, which in my
opinion best resolves usability.


==== Execution environments

What is an execution environment? An execution environment is an engine where
JavaScript can be executed. For example, the most popular execution environment is a
browser (Chrome, Firefox, ...) followed by the second most popular -
link:https://nodejs.org/[nodejs].

There are others, such as Rhino (JDK 6+), Nashorn (JDK 8+), QtQuick (QT),... but
none of them have significant differences from the first two. So,
_ClojureScript_ at the moment may compile code to run in the browser or in
nodejs-like environments out of the box.


==== Setup for NodeJS

This chapter supposes you have a properly installed nodejs (v20.10.0)
and JVM (JDK21). Other versions probably work but all this is tested
under specified versions.

It also will be nice to have the `rlwrap` tool, which can be installed
this way on a debian like linux distributions:

[source, bash]
----
sudo apt-get install rlwrap
----

So, lets create our directory stucture and the first `package.json` file:

[source, bash]
----
mkdir -p mynodeapp/src/mynodeapp
touch mynodeapp/package.json
touch mynodeapp/shadow-cljs.edn
touch mynodeapp/src/mynodeapp/main.cljs
----

Then, lets define our `package.json` content:

[source, json]
----
{
  "name": "mynodeapp",
  "version": "1.0.0",
  "description": "",
  "scripts": {
    "server": "shadow-cljs server",
    "watch": "shadow-cljs watch app",
    "compile": "shadow-cljs compile app"
  },
  "author": "",
  "license": "MPL-2.0",
  "devDependencies": {
    "shadow-cljs": "^2.26.2"
  }
}
----

And execute the `npm install` for correctly install shadow-cljs dependency

Then, setup the basic shadow-cljs.edn confguration:

[source, clojure]
----
{:dependencies
 []

 :source-paths
 ["src"]

 :builds
 {:app {:target :node-script
        :output-to "target/app.js"
        :main mynodeapp.main/main}}}
----

And finally, put the following content on the `mynodeapp/main.cljs` file:

[source, clojure]
----
(ns mynodeapp.main)

(defn main
  [& args]
  (println "Hello world!"))
----

NOTE: It is very important that the declared namespace in the file exactly matches the
directory structure. This is the way _ClojureScript_ structures its source code.

Now it's time to compile the project:

[source, bash]
----
$ npm run compile

> mynodeapp@1.0.0 compile
> shadow-cljs compile app

shadow-cljs - config: /home/user/playground/mynodeapp/shadow-cljs.edn
[:app] Compiling ...
[:app] Build completed. (45 files, 1 compiled, 0 warnings, 1.65s)
----

And when it finishes, execute the result using *node* binary:

[source, shell]
----
$ node target/main.js
Hello world!
----

The shadow-cljs guide is very extensive, and you can read more detailed documentation
here: https://shadow-cljs.github.io/docs/UsersGuide.html#target-node


==== Setup for Browser

In this section we are going to create an application similar to the "hello world"
example from the previous section to run in the browser environment. The minimal
requirement for this application is just a browser that can execute JavaScript.

The process is almost the same, and the directory structure is very similar, with few
additions.

[source, bash]
----
mkdir -p mywebapp/public
mkdir -p mywebapp/src/mywebapp
touch mywebapp/public/index.html
touch mywebapp/src/mywebapp/main.cljs
touch mywebapp/package.json
touch mywebapp/shadow-cljs.edn
----

Set setup the `package.json` file:

[source, json]
----
{
  "name": "mywebapp",
  "version": "1.0.0",
  "description": "",
  "scripts": {
    "server": "shadow-cljs server",
    "watch": "shadow-cljs watch app",
    "compile": "shadow-cljs compile app"
  },
  "author": "",
  "license": "MPL-2.0",
  "devDependencies": {
    "shadow-cljs": "^2.26.2"
  }
}
----

Execute the `npm install` for correctly install the shadow-cljs dependency.

Then, setup shadow-cljs configuration on `shadow-cljs.edn` file:

[source, clojure]
----
{:dev-http {8888 "public"}

 :dependencies
 []

 :source-paths
 ["src"]

 :builds
 {:app {:target :browser
        :output-dir "public/js"
        :asset-path "/js"
        :modules {:main {:entries [mywebapp.main]
                         :init-fn mywebapp.main/main}}}}}
----

Write new content to the `src/mywebapp/main.cljs` file:

[source, clojure]
----
(ns mywebapp.core)

(defn main
  []
  (println "Hello world!"))
----

Once all this is ready, instead of just compiling it, we start a watch process:

[source, bash]
----
$ npm run watch

> mywebapp@1.0.0 watch
> shadow-cljs watch app

shadow-cljs - config: /home/user/playground/mywebapp/shadow-cljs.edn
shadow-cljs - HTTP server available at http://localhost:8888
shadow-cljs - server version: 2.26.2 running at http://localhost:9630
shadow-cljs - nREPL server started on port 44159
shadow-cljs - watching build :app
[:app] Configuring build.
[:app] Compiling ...
[:app] Build completed. (119 files, 0 compiled, 0 warnings, 2.58s)
----

The watch also starts a development http server at http://localhost:8888 for access our
brand new web application. But we still need a last step: we need to create an index.html
where we're going to load our recently compiled js.

.On the file `public/index.html`
[source, html]
----
<!DOCTYPE html>
<html>
  <header>
    <meta charset="utf-8" />
    <title>Hello World from ClojureScript</title>
  </header>
  <body>
    <script src="js/main.js"></script>
  </body>
</html>
----

So, open the browser on http://localhost:8888 and open the devconsole with F12 key and
see the `Hello World` printed on the console tab.

The shadow-cljs guide is very extensive, and you can read more detailed documentation
here: https://shadow-cljs.github.io/docs/UsersGuide.html#target-browser


==== Working with the REPL

////
TODO: maybe it would be interesting to take some ideas from
http://www.alexeberts.com/exploring-the-clojurescript-repl/
////

Although you can create a source file and compile it every time you want to try
something out in ClojureScript, it's easier to use the REPL. REPL stands for:

* Read - get input from the keyboard
* Evaluate the input
* Print the result
* Loop back for more input

In other words, the REPL lets you try out ClojureScript concepts and get immediate
feedback.

_ClojureScript_ comes with support for executing the REPL in different execution
environments, each of which has its own advantages and disadvantages. For example, you can
run a REPL in nodejs but in that environment you don't have any access to the DOM.  Which
REPL environment is best for you depends on your specific needs and requirements.

===== Node REPL

[source, bash]
----
$ shadow-cljs node-repl
----

This starts a blank CLJS REPL with an already connected node process.

IMPORTANT: If you exit the Node REPL the node process is also killed!

The `node-repl` lets you get started without any additional configuration. It has access
to all your code via the usual means, ie. (require '[your.core :as x]). Since it is not
connected to any build it does not do any automatic rebuilding of code when your files
change and does not provide hot-reload.

===== Browser REPL


[source, bash]
----
$ shadow-cljs browser-repl
----

This starts a blank CLJS REPL and will open an associated Browser window where the code
will execute. Besides running in the Browser this has all the same functionality as the
above node-repl.

IMPORTANT: If you close the Browser window the REPL will stop working.


==== Build-specific REPL

The `node-repl` and `browser-repl` work without any specific build configuration. That
means they will only do whatever you tell them to do but nothing on their own.

But there is also an option to connect to a build specific repl, for which to work
correctly you need 2 things:

- a running watch for your build
- connect the JS runtime of the :target. Meaning if you are using the :browser target you
  need to open a Browser that has the generated JS loaded. For node.js builds that means
  running the node process.

Once you have both you can connect to the CLJS REPL via the command line or from the Clojure REPL.

[source, text]
----
# One terminal
$ npx shadow-cljs watch app
...

# different terminal
$ npx shadow-cljs cljs-repl app
shadow-cljs - connected to server
[3:1]~cljs.user=>
REPL
----

TIP: type :repl/quit to exit the REPL. This will only exit the REPL, the watch will remain running.
TIP: You may run multiple watch "workers" in parallel and connect/disconnect to their REPLs at any given time.


=== The Closure Library

The Google Closure Library is a javascript library developed by Google. It has a
modular architecture, and provides cross-browser functions for DOM manipulations and
events, ajax and JSON, and other features.

The Google Closure Library is written specifically to take advantage of the Closure
Compiler (which is used internally by the _ClojureScript_ compiler).

_ClojureScript_ is built on the Google Closure Compiler and Closure Library. In fact,
_ClojureScript_ namespaces are Closure modules. This means that you can interact with
the Closure Library very easily:

[source, clojure]
----
(ns yourapp.core
  (:require [goog.dom :as dom]))

(def element (dom/getElement "body"))
----

This code snippet shows how you can import the *dom* module of the Closure library
and use a function declared in that module.

Additionally, the closure library exposes "special" modules that behave like a class
or object. To use these features, you must use the `:import` directive in the `(ns
...)` form:

[source, clojure]
----
(ns yourapp.core
  (:import goog.History))

(def instance (History.))
----

In a _Clojure_ program, the `:import` directive is used for host (Java) interop to
import Java classes.  If, however, you define types (classes) in _ClojureScript_, you
should use the standard `:require` directive and not the `:import` directive.

You can found the reference to all namespaces in the closure library here:
http://google.github.io/closure-library/api/

To properly understand how we can use the "batteries included" of google closure library,
lets add some functionality to our mywebapp example application.

Lets update our `public/index.html` with the following content:

[source, html]
----
<!DOCTYPE html>
<html>
  <head>
    <title>leapyears</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
  </head>
  <body>
    <section class="viewport">
      <div id="result">
        ----
      </div>

      <form action="" method="">
        <label for="year">Input a year</label>
        <input id="year" name="year" />
      </form>
    </section>

    <script src="/js/main.js"></script>
  </body>
</html>
----

And then, update the `src/mywebapp/main.cljs` file with:

[source, clojure]
----
(ns mywebapp.main
  (:require
   [goog.dom :as dom]
   [goog.events :as events]
   [cljs.reader :refer [read-string]]))

(defn leap?
  [year]
  (or (zero? (js-mod year 400))
      (and (pos? (js-mod year 100))
           (zero? (js-mod year 4)))))

(defn on-change
  [event]
  (let [target (.-target event)
        result (dom/getElement "result")
        value (read-string (.-value target))]
    (if (leap? value)
      (set! (.-innerHTML result) "YES")
      (set! (.-innerHTML result) "NO"))))

(defn main
  []
  (let [input (dom/getElement "year")]
    (events/listen input "keyup" on-change)))
----

Now, we can compile the project in the same way as previously

[source, bash]
----
npm run watch
----

Finally, open the `http://locahost:8888` in a browser. Typing a year in the textbox should
display an indication of its leap year status.


=== Dependency management

Until now, we have used the builtin _Clojure(Script)_ toolchain to compile our source
files to JavaScript. Now this is a time to understand how manage external and/or third
party dependencies. Lets use our `mywebapp` to add some functionality with help of an
external dependencies.

With `shadow-cljs` we have the following approaches to add dependencies:

- Adding a native/cljs dependency
- Adding a npm dependency
- Adding a global dependency


==== Adding native dependencies

In this example we will use the link:https://github.com/funcool/cuerdas[Cuerdas] (a string
manipulation library build especifically for Clojure(Script)) for improve the previous
functionality of the *mywebapp*.

Let's update our `shadow-cljs.edn` file with the dependency:

[source, clojure]
----
{:dependencies
 [[funcool/cuerdas "2022.06.16-403"]]

 ;; [...]
 }
----

And the following modifications to the `src/mywebapp/main.cljs` file:

[source, clojure]
----
(ns mywebapp.main
  (:require
    [goog.dom :as dom]
    [goog.events :as events]
    [cuerdas.core :as str]
    [cljs.reader :refer [read-string]]))

;; [...]

(defn on-change
  [event]
  (let [target (.-target event)
        value (read-string (.-value target))]

    (if (str/blank? value)
      (set! (.-innerHTML result) "---")
      (if (leap? value)
        (set! (.-innerHTML result) "YES")
        (set! (.-innerHTML result) "NO")))))
----

Now, if you run the build or watch command, the new declared dependency will be
downloaded and the application will be compiled with this dependency included.


==== NPM Dependencies

On the other hand, not all dependencies are available as native packages. But one of the
advantages of ClojureScript is that it embraces being able to integrate with the host
language, in this case JS. For this case, shadow-cljs integrates quite well with the NPM
ecosystem.

Lets add the `date-fns` NPM dependency:

[source, bash]
----
$ npm add date-fns
----

And then, lets modify our code to use that library:


[source, clojure]
----
(ns mywebapp.main
  (:require
   ["date-fns" :as df]
   [goog.dom :as dom]
   [cuerdas.core :as str]
   [goog.events :as events]))

(defn on-change
  [event]
  (let [result (dom/getElement "result")
        target (.-target event)
        value  (.-value target)]

    (if (str/blank? value)
      (set! (.-innerHTML result) "---")
      (if (df/isLeapYear value)
        (set! (.-innerHTML result) "YES")
        (set! (.-innerHTML result) "NO")))))

(defn main
  []
  (let [input (dom/getElement "year")]
    (events/listen input "keyup" on-change)))
----

You can observe that, NPM depenencies are declared as strings on the `ns` section. Then,
you just use the `isLeapYear` exported function from the *date-fns* library like any other
function.


You can read the whole detail on how it works here:
https://shadow-cljs.github.io/docs/UsersGuide.html#npm


==== Glonal dependency

There's not much to really explain here, in this case, include whatever library is needed
from the CDN or elsewhere in the index.html and access the global export that that library
uses using clojurescript's `js/` syntax to access globally defined things. Just as you
would do, for example, to access the `location` object.

Example:

[source, clojure]
----
(js/console.log (.-href js/location))
----


=== Dealing with JS files

There are also circumstances in which it is better to use the host's native language to
implement some feature or you already have a piece of code implementing some algorithm and
you want to be able to use it from CLJS.

For this case, we have two options: an ESM module or a Closure module.


==== Closure compatible module

Lets start with a Closure module definition.

The main advantage of this approach is that there are no distinction between a
clojurescript namespace and a file defined using closure module style. You import it in
the same way independently on how it is implemented. Also if you have a library that is
written to be compatible with closure module system and you want to include it on your
project. And finally, it is the standard way how clojurescript compiler generates code
from your `.cljs` files, so it works with or without shadow-cljs tooling.

Obviously, the main disadvantage is that is uses a custom, not very common approach for
defining it.

So, for understand it correctly, lets use our `mywebapp` application and add an util
module. This is the most simplest case, because closure modules are directly compatible
with cljs and you can mix your clojurescript code with javascript code without any
additional steps.

[source, bash]
----
touch src/mywebapp/util.js
----

And then, we implement the isLeapYear in javascript:

.src/mywebapp/util.js
[source, js]
----
goog.provide("mywebapp.util");

goog.scope(function() {
  let module = mywebapp.util;

  module.isLeapYear = function(val) {
    val = parseInt(val, 10);
    return (0 === (val % 400)) || (((val % 100) > 0) && (0 === (val % 4)));
  };
});
----

Then modify the `src/mywebapp/main.cljs` file to use our *util* module:

[source, clojure]
----
(ns mywebapp.main
  (:require
   [mywebapp.util :as util]
   [goog.dom :as dom]
   [cuerdas.core :as str]
   [goog.events :as events]))

(defn on-change
  [event]
  (let [result (dom/getElement "result")
        target (.-target event)
        value  (.-value target)]

    (if (str/blank? value)
      (set! (.-innerHTML result) "---")
      (if (util/isLeapYear value)
        (set! (.-innerHTML result) "YES")
        (set! (.-innerHTML result) "NO")))))

(defn main
  []
  (let [input (dom/getElement "year")]
    (events/listen input "keyup" on-change)))
----


As you can observe, the require entry is indistinguible from any other, because it
integrates 100% with the cljs build process.


==== ESM

This is other way to define an companion modules using JS language; is the recommended way
to do it on shadow-cljs and the main advantage of it is the familiar ESM syntax.

So, lets proceed to define/overwrite the same module used in previous example to use the
ESM syntax.


.src/mywebapp/util.js
[source, js]
----
export function isLeapYear (val) {
  val = parseInt(val, 10);
  return (0 === (val % 400)) || (((val % 100) > 0) && (0 === (val % 4)));
}
----


Then, change the require on the `src/mywebapp/main.cljs` file to:

[source, clojure]
----
(ns mywebapp.main
  (:require
   ["./util.js" :as util]
   [goog.dom :as dom]
   [cuerdas.core :as str]
   [goog.events :as events]))

;; [...]
----

You can read a more detailed information about this here: https://shadow-cljs.github.io/docs/UsersGuide.html#classpath-js


=== Unit testing

As you might expect, testing in _ClojureScript_ consists of the same concepts widely
used by other language such as Clojure, Java, Python, JavaScript, etc.

Regardless of the language, the main objective of unit testing is to run some test
cases, verifying that the code under test behaves as expected and returns without
raising unexpected exceptions.

The immutablity of _ClojureScript_ data structures helps to make programs less error
prone, and facilitates testing a little bit. Another advantage of _ClojureScript_ is
that it tends to use plain data instead of complex objects. Building "mock" objects
for testing is thus greatly simplified.


==== Setup first Test

The "official" _ClojureScript_ testing framework is in the "cljs.test" namespace. It
is a very simple library, but it should be more than enough for our purposes.

There are other libraries that offer additional features or directly different
approaches to testing, such as
link:https://github.com/clojure/test.check[test.check]. However, we will not cover
them here.

We will reuse the `mywebapp` project structure and we will add testing to it. Let's create
the test file `src/mywebapp/util_test.cljs`:

[source, bash]
---
(ns mywebapp.util-test
  (:require
   [cljs.test :as t]
   ["./util.js" :as util]))

(t/deftest is-leap
  (t/is (true? (util/isLeapYear "2024"))))
----

Now, lets add a new build target to our shadow-cljs.edn configuration file:


[source, clojure]
----
{:dev-http {8888 "public"}

 :dependencies
 [[funcool/cuerdas "2022.06.16-403"]]

 :source-paths
 ["src" "test"]

 :builds
 {:app
  {:target :browser
   :output-dir "public/js"
   :asset-path "/js"
   :modules {:main {:entries [mywebapp.main]
                    :init-fn mywebapp.main/main}}}
  :test
  {:target    :node-test
   :output-to "target/test.js"
   :ns-regexp "-test$"
   :autorun   true}}}
----

Now, lets comple it and run tests:

[source, bash]
~~~~
$ npx shadow-cljs watch test
shadow-cljs - config: /home/user/playground/mywebapp/shadow-cljs.edn
shadow-cljs - HTTP server available at http://localhost:8888
shadow-cljs - server version: 2.26.2 running at http://localhost:9630
shadow-cljs - nREPL server started on port 40217
shadow-cljs - watching build :test
[:test] Configuring build.
[:test] Compiling ...
========= Running Tests =======================

Testing mywebapp.util-test

Ran 1 tests containing 1 assertions.
0 failures, 0 errors.
===============================================
[:test] Build completed. (52 files, 2 compiled, 0 warnings, 2.38s)
~~~~

It will recompile and rerun the tests on each code change.


==== Async Testing

One of the peculiarities of _ClojureScript_ is that it runs in an asynchronous,
single-threaded execution environment, which has its challenges.

In an async execution environment, we should be able to test asynchronous functions.
To this end, the _ClojureScript_ testing library offers the `async` macro, allowing
you to create tests that play well with asynchronous code.

First, we need to write a function that works in an asynchronous way. For this
purpose, we will create the `async-leap?` predicate that will do the same operation
but asychronously return a result using a callback:

[source, clojure]
----
(defn async-leap?
  [year callback]
  (js/setImmediate #(callback (util/isLeap year))))
----

The JavaScript function `setImmediate` is used to emulate an asynchronous task, and
the callback is executed with the result of that predicate.

To test it, we should write a test case using the previously mentioned `async` macro:

[source, clojure]
----
(t/deftest my-async-test
  (t/async done
    (async-leap? 1980 (fn [result]
                        (t/is (true? result))
                              (done)))))
----

The `done` function exposed by the `async` macro should be called after the
asynchronous operation is finished and all assertions have run.

It is very important to execute the `done` function only once. Omitting it or
executing it twice may cause strange behavior and should be avoided.
